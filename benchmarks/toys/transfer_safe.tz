parameter (pair (address %receiver) (mutez %value));
storage   (pair (map %balance address mutez) (mutez %totalSupply));
code {
  UNPAIR; UNPAIR; DIP 2 { UNPAIR };
  # bal_s = if sender not in balance.key then fail else balance[sender]
  DUP 3; SENDER; GET; IF_NONE { FAIL } {};
  # if not bal_s >= param.value then fail
  DUP 3; DUP 2; COMPARE; GE; IF {} { FAIL };
  # balance[sender] = bal_s - param.value
  DIP { DIG 2; DUP 3 }; SUB; SOME; SENDER; UPDATE;
  # bal_r = if param.receiver not in balance.key then 0 else balance[param.receiver]
  DUP; DUP 3; GET; IF_NONE { PUSH mutez 0 } {};
  # bal_r + param.value
  DIP { DIG 2 }; ADD;
  # check bal_r + param.value <= totalSupply
  #__MICSE_CHECK { DIP {DROP; DROP }; COMPARE; LE };
  # balance[param.receiver] = bal_r + param.value
  SOME; DIG 2; UPDATE;
  PAIR; NIL operation; PAIR };